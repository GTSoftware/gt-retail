plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'war'
    id 'com.github.node-gradle.node' version '2.2.3'
    id 'com.github.sherter.google-java-format' version '0.8'
}

group = 'ar.com.gtsoftware'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }

    jasperreports {
        transitive = true
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    //implementation 'org.springframework.boot:spring-boot-dependency-tools'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.ws:spring-ws-security'
    implementation 'org.springframework.ws:spring-ws-core'
    testImplementation 'org.springframework.ws:spring-ws-test'
    implementation 'org.glassfish.jaxb:jaxb-runtime'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql:42.2.11'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-collections4:4.4'
    compileOnly 'org.hibernate:hibernate-jpamodelgen:5.4.12.Final'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
    compileOnly 'org.mapstruct:mapstruct:1.3.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.65'
    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.65'
    implementation 'net.sf.jasperreports:jasperreports:6.9.0'
    implementation 'net.sf.jasperreports:jasperreports-fonts:6.0.0'
    implementation 'com.lowagie:itext:2.1.7'
    implementation 'net.sf.barcode4j:barcode4j:2.1'
    implementation 'org.apache.xmlgraphics:batik-bridge:1.13'
    implementation 'org.jeasy:easy-rules-core:4.0.0'
    implementation 'org.jeasy:easy-rules-mvel:4.0.0'
    implementation 'org.apache.poi:poi-ooxml:5.0.0'

    jasperreports 'com.lowagie:itext:2.1.7'
    jasperreports 'net.sf.barcode4j:barcode4j:2.1'
    implementation 'com.google.zxing:core:3.3.2'
    jasperreports 'net.sf.jasperreports:jasperreports:6.9.0'

}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}


task compileJasperJava(dependsOn: 'compileJava') {
    def jasperSourceDir = file('src/main/resources/reports')
    def jasperTargetDir = file('build/resources/main/reports')
    ant {
        taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.jasperreports.asPath)
        jasperTargetDir.mkdirs()
        jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
            classpath(path: sourceSets.main.output.classesDirs.asPath)
            include(name: '**/*.jrxml')
        }
    }
}
classes.dependsOn compileJasperJava

task copyWebApp(type: Copy) {
    from 'webapp/build'
    into 'build/resources/main/static/.'
}

task appNpmInstall(type: NpmTask) {
    description = "Installs all dependencies from package.json"
    workingDir = file("${project.projectDir}/webapp")
    args = ["install"]
}

task appNpmBuild(type: NpmTask) {
    description = "Builds production version of the webapp"
    workingDir = file("${project.projectDir}/webapp")
    args = ["run", "build"]
}

appNpmBuild.dependsOn appNpmInstall
copyWebApp.dependsOn appNpmBuild
//compileJava.dependsOn copyWebApp

node {
    download = true
    // Version of node to use.
    version = '13.12.0'

    // Version of npm to use.
    npmVersion = '6.14.4'
    // Set the work directory for unpacking node
    //workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    //npmWorkDir = file("${project.buildDir}/npm")

}
